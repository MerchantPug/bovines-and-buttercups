import org.apache.tools.ant.filters.LineContains
import net.darkhax.curseforgegradle.TaskPublishCurseForge

buildscript {
    repositories {
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.72'
    id 'net.neoforged.gradle.mixin' version '7.0.72'
    id "net.darkhax.curseforgegradle" version "1.0.7"
    id "com.modrinth.minotaur" version "2.+"
    id 'java-library'
}

archivesBaseName = "${artifact_name}-NeoForge"

mixin {
    config "${project.mod_id}.mixins.json"
    config "${project.mod_id}.neoforge.mixins.json"
}

if (file('src/main/resources/META-INF/accesstransformer.cfg').exists()) {
    minecraft.accessTransformers.file file('src/main/resources/META-INF/accesstransformer.cfg')
}

runs {
    configureEach {
        workingDirectory project.file("run")
        modSource project.sourceSets.main
        modSource project.sourceSets.test
    }
    client {
        systemProperty 'forge.enabledGameTestNamespaces', mod_id
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', mod_id
        programArgument '--nogui'
    }
}

minecraft {
    mappings {
        channel = official()
        version.put "minecraft", minecraft_version
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

repositories {
    maven {
        // location of the maven that hosts JEI files since January 2023
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        url 'https://maven.terraformersmc.com/'
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
    maven { url "https://maven.shedaniel.me/" }
    maven {
        name = 'Modrinth'
        url = 'https://api.modrinth.com/maven'
        content {
            includeGroup 'maven.modrinth'
        }
    }
    mavenCentral()
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    implementation "net.neoforged:neoforge:${neoforge_version}"

    compileOnly group: 'com.google.auto.service', name: 'auto-service', version: '1.0.1'
    annotationProcessor group: 'com.google.auto.service', name: 'auto-service', version: '1.0.1'

    compileOnly project(":common")

    compileOnly("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}")
    compileOnly("mezz.jei:jei-${minecraft_version}-neoforge-api:${jei_version}")
    // TODO: For some odd reason the below two lines are necessary to compile, feel free to PR an easier method if you know one.
    testCompileOnly("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}")
    testCompileOnly("mezz.jei:jei-${minecraft_version}-neoforge-api:${jei_version}")

    runtimeOnly("mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}")

    // TODO: Use REI API jar once https://github.com/shedaniel/RoughlyEnoughItems/issues/1194 is fixed.
    /*
    compileOnly "me.shedaniel:RoughlyEnoughItems-neoforge:${rei_version}"
    compileOnly "me.shedaniel.cloth:cloth-config-neoforge:${cloth_config_version}"
    compileOnly "dev.architectury:architectury-neoforge:${architectury_version}"

    runtimeOnly "me.shedaniel:RoughlyEnoughItems-neoforge:${rei_version}"
    runtimeOnly "me.shedaniel.cloth:cloth-config-neoforge:${cloth_config_version}"
    runtimeOnly "dev.architectury:architectury-neoforge:${architectury_version}"
     */

    compileOnly "dev.emi:emi-neoforge:${emi_version}:api"
    // TODO: For some odd reason the below line is necessary to compile, feel free to PR an easier method if you know one.
    testCompileOnly("dev.emi:emi-neoforge:${emi_version}:api")
    runtimeOnly "dev.emi:emi-neoforge:${emi_version}"
}

// NeoGradle compiles the game, but we don't want to add our common code to the game's code
TaskCollection.metaClass.excludingNeoTasks = { ->
    delegate.matching { !it.name.startsWith("neo") }
}

tasks.withType(JavaCompile).excludingNeoTasks().configureEach {
    source(project(":common").sourceSets.main.allSource)
    exclude "net/merchantpug/bovinesandbuttercups/integration/jei/**"
    exclude "net/merchantpug/bovinesandbuttercups/client/integration/emi/**"
}

tasks.withType(Javadoc).excludingNeoTasks().configureEach {
    source(project(":common").sourceSets.main.allJava)
    exclude "net/merchantpug/bovinesandbuttercups/integration/jei/**"
    exclude "net/merchantpug/bovinesandbuttercups/client/integration/emi/**"
}

tasks.named("sourcesJar", Jar) {
    from(project(":common").sourceSets.main.allSource)
}

tasks.withType(ProcessResources).excludingNeoTasks().configureEach {
    from project(":common").sourceSets.main.resources
    exclude("bovinesandbuttercups-common.accesswidener")

    filesMatching("bovinesandbuttercups.mixins.json") {
        filter(LineContains, negate: true, contains: [ 'refmap' ] )
    }
}

tasks.named("processTestResources") {
    from project(":common").sourceSets.test.resources
}

jar {
    manifest {
        attributes(
                "Implementation-Version": "${project.version}-neoforge"
        )
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            artifact jar
        }
    }

    repositories {
        maven {
            name = "merchantpug"
            url = "https://maven.merchantpug.net/releases"
            credentials {
                username System.getenv('MAVEN_USERNAME')
                password System.getenv('MAVEN_PASSWORD')
            }
            authentication {
                create("basic", BasicAuthentication)
            }
        }
    }
}

tasks.register('curseforge', TaskPublishCurseForge) {
    disableVersionDetection()
    apiToken = System.getenv("CURSEFORGE_TOKEN")

    def mainFile = upload(cf_project_id, jar)
    mainFile.displayName = "NeoForge v${rootProject.version}}"
    mainFile.releaseType = "release"
    mainFile.changelog = new File("$project.rootDir/CHANGELOG.md").getText('UTF-8')
    mainFile.changelogType = "markdown"
    mainFile.addModLoader('NeoForge')
    neoforge_game_versions.split(", ").each {
        mainFile.addGameVersion(it)
    }
    java_versions.split(", ").each {
        mainFile.addJavaVersion(it)
    }

    // TODO: Uncomment this once JEI/EMI is on NeoForge
    /*
    common_optional_deps.split(", ").each {
        mainFile.addOptional(it)
    }
     */
    cf_common_optional_deps.split(", ").each {
        mainFile.addOptional(it)
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = mr_project_id
    versionNumber = "${project.version}-neoforge"
    versionType = "release"
    versionName = "NeoForge v${rootProject.version}"
    changelog = new File("$project.rootDir/CHANGELOG.md").getText('UTF-8')
    uploadFile = jar
    gameVersions = neoforge_game_versions.split(", ").toList()
    loaders = ["neoforge"]
    dependencies {

    }
    // TODO: Uncomment this once JEI/EMI is on NeoForge
    /*
    common_optional_deps.split(", ").each {
        optional.project it
    }
     */
    mr_common_optional_deps.split(", ").each {
        optional.project it
    }
}